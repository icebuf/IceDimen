apply plugin: 'maven'
apply plugin: 'signing'

// 源代码一起打包
task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

artifacts {
    archives androidSourcesJar, androidJavadocsJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    loadProperties('../local.properties')
    def username = getProperty('nexus.username')
    def password = getProperty('nexus.password')
//    println(project.android.sourceSets.main.java.sourceFiles)
    println(getProperty('maven.version'))
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }
            repository(url: getProperty('maven.repo')) {
                authentication(userName: username, password: password)
            }
            snapshotRepository(url: getProperty('maven.snapshot.repo')) {
                authentication(userName: username, password: password)
            }
            pom.project {
                name 'IceDimen'
                groupId getProperty('maven.groupId')
                artifactId  getProperty('maven.artifactId')
                version getProperty('maven.version')
//                packaging 'aar'
                description 'This is a useful library for Android to fit screen.'
                url 'https://github.com/icebuf/IceDimen'
                scm {
                    connection 'scm:git:https://github.com/icebuf/IceDimen.git'
                    developerConnection 'scm:git:https://github.com/icebuf/IceDimen.git'
                    url 'https://github.com/icebuf/IceDimen.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'icebuf'
                        name 'IceTang'
                        email '1004859421@qq.com'
                    }
                }
            }
        }
    }
}



def loadProperties(def path){
    File file = project.file(path)
    if(file.exists()){
        InputStream inputStream = file.newDataInputStream();
        Properties properties = new Properties()
        properties.load(inputStream)
        for (def entry : properties.entrySet()) {
            try {
                setProperty(entry.key, entry.value)
                println(entry.key + " " + entry.value)
            } catch (MissingPropertyException ignore) {
            }
        }
    }
}